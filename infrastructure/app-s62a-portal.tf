module "app_s62a_portal" {
  #checkov:skip=CKV_TF_1: Use of commit hash are not required for our Terraform modules
  source = "github.com/Planning-Inspectorate/infrastructure-modules.git//modules/node-app-service?ref=1.54"

  resource_group_name = azurerm_resource_group.primary.name
  location            = module.primary_region.location

  # naming
  app_name        = "s62a-portal"
  resource_suffix = var.environment
  service_name    = local.service_name

  tags = merge(
    local.tags,
    var.environment == "prod" ? local.resource_tags["linux_web_app_s62a_portal"] : {}
  )

  # service plan & scaling
  app_service_plan_id                  = azurerm_service_plan.apps.id
  app_service_plan_resource_group_name = azurerm_resource_group.primary.name
  worker_count                         = var.apps_config.app_service_plan.worker_count # match the app service plan

  # container
  container_registry_name = var.tooling_config.container_registry_name
  container_registry_rg   = var.tooling_config.container_registry_rg
  image_name              = "crown/s62a-portal"

  # networking
  app_service_private_dns_zone_id = data.azurerm_private_dns_zone.app_service.id
  inbound_vnet_connectivity       = var.apps_config.private_endpoint_enabled
  integration_subnet_id           = azurerm_subnet.apps.id
  endpoint_subnet_id              = azurerm_subnet.main.id
  outbound_vnet_connectivity      = true
  # public access via Front Door
  front_door_restriction = true
  public_network_access  = true

  # monitoring
  action_group_ids                  = local.action_group_ids
  log_analytics_workspace_id        = azurerm_log_analytics_workspace.main.id
  monitoring_alerts_enabled         = var.alerts_enabled
  health_check_path                 = "/health"
  health_check_eviction_time_in_min = var.health_check_eviction_time_in_min

  #Easy Auth setting
  auth_config = {
    # Using same credentials as the main portal app
    auth_enabled           = var.auth_config_portal.auth_enabled
    require_authentication = var.auth_config_portal.auth_enabled
    auth_client_id         = var.auth_config_portal.auth_client_id
    #checkov:skip=CKV_SECRET_6: "Secret is securely stored in Key Vault"
    auth_provider_secret = "MICROSOFT_PROVIDER_AUTHENTICATION_SECRET"
    auth_tenant_endpoint = "https://login.microsoftonline.com/${data.azurerm_client_config.current.tenant_id}/v2.0"
    allowed_applications = var.auth_config_portal.application_id
    allowed_audiences    = "https://${var.web_domains.s62a_portal}/.auth/login/aad/callback"
    excluded_paths       = []
  }

  app_settings = {
    APPLICATIONINSIGHTS_CONNECTION_STRING      = local.key_vault_refs["app-insights-connection-string"]
    ApplicationInsightsAgent_EXTENSION_VERSION = "~3"
    APP_HOSTNAME                               = var.web_domains.s62a_portal
    NODE_ENV                                   = var.apps_config.node_environment
    ENVIRONMENT                                = var.environment

    # logging
    LOG_LEVEL = var.apps_config.logging.level

    # database connection
    SQL_CONNECTION_STRING = local.key_vault_refs["sql-app-connection-string"]

    # sessions
    REDIS_CONNECTION_STRING  = local.key_vault_refs["redis-connection-string"]
    SESSION_SECRET_PRIMARY   = time_rotating.s62a_portal_session_secret_a.unix > time_rotating.s62a_portal_session_secret_b.unix ? local.key_vault_refs["s62a_portal_session_secret_a"] : local.key_vault_refs["s62a_portal_session_secret_b"]
    SESSION_SECRET_SECONDARY = time_rotating.s62a_portal_session_secret_a.unix > time_rotating.s62a_portal_session_secret_b.unix ? local.key_vault_refs["s62a_portal_session_secret_b"] : local.key_vault_refs["s62a_portal_session_secret_a"]


    # retries
    RETRY_MAX_ATTEMPTS = "3"
    # got default retry codes
    # https://github.com/sindresorhus/got/blob/main/documentation/7-retry.md
    RETRY_STATUS_CODES = "408,413,429,500,502,503,504,521,522,524"

    #Auth
    MICROSOFT_PROVIDER_AUTHENTICATION_SECRET = local.key_vault_refs["microsoft-provider-authentication-secret"]
    WEBSITE_AUTH_AAD_ALLOWED_TENANTS         = data.azurerm_client_config.current.tenant_id

    # gov notify
    GOV_NOTIFY_DISABLED = var.apps_config.gov_notify.disabled
    GOV_NOTIFY_API_KEY  = local.key_vault_refs["crown-gov-notify-api-key"] # this will be changed to s62a specific key in future

    CROWN_DEV_CONTACT_EMAIL = var.apps_config.contact_email

    #feature flags
    FEATURE_FLAG_S62A_PORTAL_NOT_LIVE      = var.apps_config.feature_flags.s62a_portal_not_live
    FEATURE_FLAG_UPLOAD_DOCS_REPS_NOT_LIVE = var.apps_config.feature_flags.upload_docs_not_live # this flag needs to be unique to s62a

    # Cache Controls
    DYNAMIC_CACHE_CONTROL_ENABLED = var.apps_config.dynamic_cache_control.enabled
    DYNAMIC_CACHE_CONTROL_MAX_AGE = var.apps_config.dynamic_cache_control.max_age

    # Google Analytics
    GOOGLE_ANALYTICS_ID = var.apps_config.google_analytics_id

    # blob store
    BLOB_STORE_DISABLED  = var.apps_config.blob_store.disabled
    BLOB_STORE_HOST      = azurerm_storage_account.crown_documents.primary_blob_endpoint
    BLOB_STORE_CONTAINER = azurerm_storage_container.documents.name
  }

  providers = {
    azurerm         = azurerm
    azurerm.tooling = azurerm.tooling
  }
}

## RBAC for secrets
resource "azurerm_role_assignment" "app_s62a_portal_secrets_user" {
  scope                = azurerm_key_vault.main.id
  role_definition_name = "Key Vault Secrets User"
  principal_id         = module.app_s62a_portal.principal_id
}

## RBAC for secrets (staging slot)
resource "azurerm_role_assignment" "app_s62a_portal_staging_secrets_user" {
  scope                = azurerm_key_vault.main.id
  role_definition_name = "Key Vault Secrets User"
  principal_id         = module.app_s62a_portal.staging_principal_id
}

## sessions
#https://2mas.github.io/blog/rotating-azure-app-registration-secrets-with-terraform/
# Two rotators, offset by half of the rotation period
resource "time_rotating" "s62a_portal_session_secret_a" {
  rotation_months = 6
}

resource "time_rotating" "s62a_portal_session_secret_b" {
  rfc3339         = timeadd(time_rotating.s62a_portal_session_secret_a.rfc3339, "2160h") # + 3 months
  rotation_months = 6

  lifecycle {
    ignore_changes = [rfc3339]
  }
}

# Two random passwords, one for each rotator, to be rotated by the rotators
resource "random_password" "s62a_portal_session_secret_a" {
  length  = 32
  special = true
  keepers = {
    rotation = time_rotating.s62a_portal_session_secret_a.id
  }
}

resource "azurerm_key_vault_secret" "s62a_portal_session_secret_a" {
  key_vault_id    = azurerm_key_vault.main.id
  name            = "${local.service_name}-s62a-portal-session-secret-a"
  value           = random_password.s62a_portal_session_secret_a.result
  content_type    = "session-secret"
  expiration_date = time_rotating.s62a_portal_session_secret_a.rotation_rfc3339

  depends_on = [
    azurerm_private_endpoint.keyvault,
    azurerm_private_dns_zone_virtual_network_link.keyvault
  ]

  tags = merge(
    local.tags,
    var.environment == "prod" ? local.resource_tags["key_vault_secret_s62a_portal_session_secret"] : {}
  )
}

resource "random_password" "s62a_portal_session_secret_b" {
  length  = 32
  special = true
  keepers = {
    rotation = time_rotating.s62a_portal_session_secret_b.id
  }
}

resource "azurerm_key_vault_secret" "s62a_portal_session_secret_b" {
  key_vault_id    = azurerm_key_vault.main.id
  name            = "${local.service_name}-s62a-portal-session-secret-b"
  value           = random_password.s62a_portal_session_secret_b.result
  content_type    = "session-secret"
  expiration_date = time_rotating.s62a_portal_session_secret_b.rotation_rfc3339

  depends_on = [
    azurerm_private_endpoint.keyvault,
    azurerm_private_dns_zone_virtual_network_link.keyvault
  ]

  tags = merge(
    local.tags,
    var.environment == "prod" ? local.resource_tags["key_vault_secret_s62a_portal_session_secret"] : {}
  )
}
